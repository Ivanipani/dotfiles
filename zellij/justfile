set unstable

config_home := "$HOME/.config/zellij"
pwd := absolute_path(".")
invocation_dir := invocation_directory()

# Display this help
default:
  @just --list --unsorted

# Start or switch zellij sessions (nushell version)
[no-cd]
sessionizer:
  nu {{pwd}}/scripts/sessionizer.nu

# Start or switch zellij sessions (zsh version)
[no-cd]
sessionizer-zsh:
  zsh {{pwd}}/scripts/sessionizer.zsh

# Configure zellij
setup: create-symlinks

install: install-plugins
    cargo install --locked zellij

# Create symlinks to the config files
create-symlinks:
    mkdir -p {{config_home}}
    ln -sf {{pwd}}/config.kdl {{config_home}}

    rm -rf {{config_home}}/layouts
    ln -sf {{pwd}}/layouts {{config_home}}/layouts

    rm -rf {{config_home}}/plugins
    ln -sf {{pwd}}/plugins {{config_home}}/plugins

# Install plugins
[script("zsh")]
install-plugins: create-symlinks
  if [ ! -f ./plugins/vim-zellij-navigator.wasm ]; then
    wget -P ./plugins https://github.com/hiasr/vim-zellij-navigator/releases/download/0.3.0/vim-zellij-navigator.wasm
  else
    echo "vim-zellij-navigator plugin is already installed"
  fi
  if [ ! -f ./plugins/zellij-autolock.wasm ]; then
    wget -P ./plugins https://github.com/fresh2dev/zellij-autolock/releases/download/0.2.2/zellij-autolock.wasm
  else
    echo "zellij-autolock plugin is already installed"
  fi
  if [ ! -f ./plugins/zellij-sessionizer.wasm ]; then
    wget -P ./plugins https://github.com/laperlej/zellij-sessionizer/releases/download/v0.4.3/zellij-sessionizer.wasm
  else
    echo "zellij-sessionizer plugin is already installed"
  fi
  if [ ! -f ./plugins/zellij-switch.wasm ]; then
    wget -P ./plugins https://github.com/mostafaqanbaryan/zellij-switch/releases/download/0.2.1/zellij-switch.wasm
  else
    echo "zellij-switch plugin is already installed"
  fi
