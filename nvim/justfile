set unstable

config_home := "$HOME/.config"
pwd := absolute_path(".")

# Display this help
default:
  @just --list --unsorted

# Configure neovim
setup: create-symlinks

# Create symlinks to the config files
create-symlinks:
    mkdir -p {{config_home}}
    rm -rf {{config_home}}/nvim
    ln -sf {{pwd}}/config {{config_home}}/nvim

install: install-neovim install-language-servers

# Install neovim
[macos]
install-neovim:
    brew install neovim

# Install neovim
[linux]
install-neovim:
    sudo apt install neovim

# Install language servers
[macos]
install-language-servers:
    rustup component add rust-analyzer
    uv tool install --with rope --with pylsp-rope --with python-lsp-ruff --with pylsp-mypy python-lsp-server
    brew install lua-language-server
